@PACKAGE_INIT@

set(VecCoreLib_VERSION @VecCoreLib_VERSION@)
set(VecCoreLib_VERSION_MAJOR @VecCoreLib_VERSION_MAJOR@)
set(VecCoreLib_VERSION_MINOR @VecCoreLib_VERSION_MINOR@)
set(VecCoreLib_VERSION_PATCH @VecCoreLib_VERSION_PATCH@)

set_and_check(VecCoreLib_INCLUDE_DIR "@PACKAGE_VecCoreLib_INSTALL_INCLUDEDIR@")
set_and_check(VecCoreLib_CMAKE_DIR "${CMAKE_CURRENT_LIST_DIR}")

set(VecCoreLib_INCLUDE_DIRS "${VecCoreLib_INCLUDE_DIR}")
list(APPEND CMAKE_MODULE_PATH "${VecCoreLib_CMAKE_DIR}")

set(_VecCoreLib_FIND_QUIET)
if (VecCoreLib_FIND_QUIETLY)
  set(_VecCoreLib_FIND_QUIET QUIET)
endif()

set(_VecCoreLib_NOT_FOUND_MESSAGE)

find_package(VecCore 0.4.2 ${_VecCoreLib_FIND_QUIET})
if (NOT VecCore_FOUND)
  if(VecCoreLib_FIND_REQUIRED_VecCore)
    set(_VecCoreLib_NOT_FOUND_MESSAGE ${_VecCoreLib_NOT_FOUND_MESSAGE}
      "Failed to find VecCoreLib component \"VecCore\"\n")
  elseif(NOT VecCoreLib_FIND_QUIETLY)
    message(WARNING "Failed to find VecCoreLib component \"VecCore\"")
  endif()
else()
  set(VecCoreLib_VecCore_FOUND True)
  set(VecCoreLib_VecCore_INCLUDE_DIR ${VecCore_INCLUDE_DIR})
  set(VecCoreLib_VecCore_LIBRARIES ${VecCoreLib_LIBRARIES} ${VecCoreLib_LIBRARIES})
endif()

if (_VecCoreLib_NOT_FOUND_MESSAGE)
  set(VecCoreLib_FOUND False)
  set(VecCoreLib_NOT_FOUND_MESSAGE ${_VecCoreLib_NOT_FOUND_MESSAGE})
  return()
endif()

set(VecCoreLib_DEFINITIONS ${VecCoreLib_DEFINITIONS} ${VecCore_DEFINITIONS})
set(VecCoreLib_INCLUDE_DIRS ${VecCoreLib_INCLUDE_DIRS} ${VecCore_INCLUDE_DIR})
set(VecCoreLib_LIBRARIES ${VecCoreLib_LIBRARIES} ${VecCore_LIBRARIES})

list(REMOVE_DUPLICATES VecCoreLib_INCLUDE_DIRS)

check_required_components(VecCoreLib)

add_library(VecCoreLib::VecCoreLib INTERFACE IMPORTED)
set_target_properties(VecCoreLib::VecCoreLib PROPERTIES
  INTERFACE_INCLUDE_DIRECTORIES "${VecCoreLib_INCLUDE_DIRS}")

if (VecCoreLib_DEFINITIONS)
  string(REGEX REPLACE "-D" "" _localdefs "${VecCoreLib_DEFINITIONS}")
  set_target_properties(VecCoreLib::VecCoreLib PROPERTIES
    INTERFACE_COMPILE_DEFINITIONS "${_localdefs}")
  unset(_localdefs)
endif()

if (VecCoreLib_LIBRARIES)
  set_target_properties(VecCoreLib::VecCoreLib PROPERTIES
    INTERFACE_LINK_LIBRARIES "${VecCoreLib_LIBRARIES}")
endif()
